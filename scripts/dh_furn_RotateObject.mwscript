begin dh_furn_RotateObject
	; Displays a message to pickup, rotate or move up/down the object.  Rotates/Moves the object as
	; required until the user activates it again, or the set maximum amount of movement is exceeded
	; (to try and prevent the object from 'running away').  Used for simple objects like cushions which
	; the player might want to rearrange in different orientations.

  short	messageOn
  short     IsRotating
  short     IsMovingUp
  short     IsMovingDown
  short	button
  short	NoLore
  float		OriginalPosition
  float		MaxPosition
  float		NewPosition


	; Ignore if currently in menu mode
if ( MenuMode == 1)
	return
endif

	; Check for object activation
if ( OnActivate == 1 )

		; Do nothing if the script has been disabled
	if (g_dhfurn_EnableMove == 0)
		return
	endif

	Set messageOn to 2
endif

	; Check for doing nothing to exit script early, hopefully speeding it
if (messageOn == 0)
	return
endif

	; Initial object activation 
if ( messageOn == 2 )
     
			; Turn off rotating if required
     if (IsRotating == 1) 
		set IsRotating to 0
		set messageOn to 0
		return
     endif

			; Turn off moving up required
     if (IsMovingUp == 1) 
		set IsMovingUp to 0
		set messageOn to 0
		return
     endif

			; Turn off moving down required
     if (IsMovingDown == 1) 
		set IsMovingDown to 0
		set messageOn to 0
		return
     endif
	
		; Display message box with choices for the player
	MessageBox "What do you want to do with this object?"  "   Pick it up    " "   Rotate It    " "    Move Up     "  "  Move Down   " "     Nothing      "
	Set messageOn to 1
endif

	; Process the player's choice from the message box
if ( messageOn == 1 )
	set button to GetButtonPressed

		; Pickup object
	if ( button == 0 )
		Activate
		Set messageOn to 0
		return
	endif

		; Rotate object
	if ( button == 1 )
		Set IsRotating to 1
		Set messageOn to 10
		return
	endif

		; Move object up
	if ( button == 2 )
		Set OriginalPosition to GetPos, Z
		Set MaxPosition to OriginalPosition + g_dhfurn_MaxMove
		Set IsMovingUp to 1
		Set messageOn to 10
		return
	endif

		; Move object down
	if ( button == 3 )
		Set OriginalPosition to GetPos, Z
		Set MaxPosition to OriginalPosition - g_dhfurn_MaxMove
		Set IsMovingDown to 1
		Set messageOn to 10
		return
	endif

		; Do nothing
	if ( button == 4 )
		Set messageOn to 0
		return
	endif

endif

	; Rotate/move object as required
if (IsRotating == 1)
	Rotate y, 20.0
endif

		; Move up
if (IsMovingUp == 1)
	set NewPosition to GetPos, Z

			; Ensure the object doesn't 'run away' on us
	if (NewPosition  > MaxPosition)
		set messageOn to 0
		set IsMovingUp to 0
		return
	endif

	MoveWorld z, 0.3
endif

		; Move down
if (IsMovingDown == 1)
	set NewPosition to GetPos, Z

			; Ensure the object doesn't 'run away' on us
	if (NewPosition  < MaxPosition)
		set messageOn to 0
		set IsMovingDown to 0
		return
	endif

	MoveWorld z, -0.3
endif

end